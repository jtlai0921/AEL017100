Version=3.2
IconFile=
NumberOfModules=0
Package=appb_2_1.gui.b4a.example
DoNotOverwriteManifest=False
ManifestCode='This code will be applied to the manifest file during compilation.~\n~'You do not need to modify it in most cases.~\n~'See this link for for more information: http://www.basic4ppc.com/forum/showthread.php?p=78136~\n~AddManifestText(~\n~<uses-sdk android:minSdkVersion="4" android:targetSdkVersion="14"/>~\n~<supports-screens android:largeScreens="true" ~\n~    android:normalScreens="true" ~\n~    android:smallScreens="true" ~\n~    android:anyDensity="true"/>)~\n~SetApplicationAttribute(android:icon, "@drawable/icon")~\n~SetApplicationAttribute(android:label, "$LABEL$")~\n~'End of default text.~\n~
UserTypesHint=NameMac
NumberOfFiles=1
File1=main.bal
NumberOfLibraries=2
Library1=core
Library2=serial
@EndOfDesignText@
#Region  Project Attributes 
	#ApplicationLabel: 連接藍芽裝置
	#VersionCode: 1
	#VersionName: 
	'SupportedOrientations possible values: unspecified, landscape or portrait.
	#SupportedOrientations: unspecified
	#CanInstallToExternalStorage: False
#End Region

#Region  Activity Attributes 
	#FullScreen: False
	#IncludeTitle: True
#End Region

Sub Process_Globals
    Dim bluAdmin As BluetoothAdmin
    Dim bluSPP As Serial
    Dim btDevices As List
    Type NameMac (name As String, mac As String)
    Dim connected As Boolean
End Sub

Sub Globals
    Dim spnrPairedDevices As Spinner
    Dim btnConnectBT As Button
    Dim lblBTStatus As Label
    Dim btnDisconnectBT As Button
    Dim txtLog As EditText
End Sub

Sub Activity_Create(FirstTime As Boolean)
    If FirstTime Then
        btDevices.Initialize()       ' 初始List物件
        bluAdmin.Initialize("Admin") ' 初始BluetoothAdmin物件
        bluSPP.Initialize("Serial1") ' 初始Serial物件	    
        FindPairedDevices
    End If
    Activity.LoadLayout("Main")
    Activity.Title = "連接藍芽裝置"
    txtLog.Width = 100%x    ' 最大寬度
    txtLog.Text = "MAC地址: " & bluSPP.Address & CRLF & _
	              "名稱: " & bluSPP.Name
    connected = False
End Sub

Sub Activity_Resume    
    If bluSPP.IsEnabled = False Then
        Msgbox("請先啟用裝置的藍芽....", "連接藍芽裝置")
		Activity.Finish()
    Else
        UpdateSpinnerItem   ' 更新介面
        UpdateUI   
    End If   
End Sub

Sub Activity_Pause (UserClosed As Boolean)

End Sub
' 取得配對裝置清單
Sub FindPairedDevices
    btDevices.Clear()     ' 清除裝置清單
    ' 取得配對裝置
    Dim pairedDevices As Map = bluSPP.GetPairedDevices
    For i = 0 To pairedDevices.Size - 1  ' 新增配對裝置
        Dim nm As NameMac
        nm.Initialize()
        nm.mac = pairedDevices.GetValueAt(i)
        nm.name = pairedDevices.GetKeyAt(i)
        btDevices.Add(nm)  ' 新增至裝置清單
    Next
End Sub
' 更新Spinner元件項目
Sub UpdateSpinnerItem
    spnrPairedDevices.Clear()      ' 清除Spinner元件的項目
    For Each nm As NameMac In btDevices   ' 新增配對裝置至Spinner元件
        spnrPairedDevices.Add(nm.name)
    Next 
End Sub 
' 更新使用介面
Public Sub UpdateUI
    ' 沒有配對裝置
    If spnrPairedDevices.Size = 0 Then
        btnConnectBT.Enabled = False
    End If	
	If connected Then    ' 是否已經連接
        btnConnectBT.Enabled = False
        btnDisconnectBT.Enabled = True
    Else 
        btnConnectBT.Enabled = True
        btnDisconnectBT.Enabled = False
    End If
End Sub
' 連接藍芽裝置
Sub btnConnectBT_Click
    ' 沒有選擇裝置
    If spnrPairedDevices.SelectedIndex = -1 Then Return
    Dim nm As NameMac = btDevices.Get(spnrPairedDevices.SelectedIndex)
    bluSPP.Connect(nm.mac)  ' 建立藍芽連接
    lblBTStatus.Text = "正在連接藍芽裝置..."
    btnConnectBT.Enabled = False
End Sub
' 已經成功連接藍芽裝置
Sub Serial1_Connected(Success As Boolean)
    If Success = False Then    ' 失敗
        ToastMessageShow("藍芽連接錯誤: " & LastException.Message, True)
        lblBTStatus.Text = "藍芽連接錯誤..."
        btnConnectBT.Enabled = True
        connected = False
    Else                       ' 成功
        lblBTStatus.Text = "成功建立藍芽連接..."
        btnConnectBT.Enabled = False
        btnDisconnectBT.Enabled = True
        connected = True
    End If
End Sub
' 中斷藍芽連接
Sub btnDisconnectBT_Click
    bluSPP.Disconnect()   ' 中斷連接
    btnConnectBT.Enabled = True
    btnDisconnectBT.Enabled = False
    lblBTStatus.Text = "藍芽連接已經中斷..."
End Sub